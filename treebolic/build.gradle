/*
 * Copyright (c) Treebolic 2019. Bernard Bou <1313ou@gmail.com>
 */

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    namespace "org.treebolic"

    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "org.treebolic"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        treebolic {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.treebolic
            versionNameSuffix 'signed'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'io.github.treebolic:treebolic-graph:4.0-1'
    implementation 'io.github.treebolic:treebolic-mutable:4.0-1'
    implementation 'io.github.treebolic:treebolic-model:4.0-1'
    implementation 'io.github.treebolic:treebolic-view:4.0-1'
    implementation 'io.github.treebolic:provider-xml-dom:4.0-1'
    implementation 'io.github.treebolic:provider-xml-sax:4.0-1'
    implementation 'io.github.treebolic:provider-text-indent-tree:4.0-1'
    implementation 'io.github.treebolic:provider-text-indent:4.0-1'
    implementation 'io.github.treebolic:provider-text-pair:4.0-1'

    implementation project(':treebolicGlue')
    implementation project(':treebolicParcel')
    implementation project(':treebolicIface')
    implementation project(':treebolicClientsIface')
    implementation project(':treebolicClientsLib')

    implementation project(':commonLib')
    implementation project(':storageLib')
    implementation project(':searchLib')
    implementation project(':fileChooserLib')
    implementation project(':downloadLib')
    implementation project(':preferenceLib')
    implementation project(':guideLib')
    implementation project(':rateLib')
    implementation project(':othersLib')
    implementation project(':donateLib')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.10.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation "androidx.multidex:multidex:2.0.1"

    testImplementation 'androidx.test.ext:junit:1.1.5'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}