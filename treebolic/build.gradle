/*
 * Copyright (c) Treebolic 2019. Bernard Bou <1313ou@gmail.com>
 */

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    namespace "org.treebolic"

    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "org.treebolic"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        treebolic {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.treebolic
            versionNameSuffix 'signed'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation "io.github.treebolic:treebolic-graph:${treebolicVersion}"
    implementation "io.github.treebolic:treebolic-mutable:${treebolicVersion}"
    implementation "io.github.treebolic:treebolic-model:${treebolicVersion}"
    implementation "io.github.treebolic:treebolic-view:${treebolicVersion}"
    implementation "io.github.treebolic:provider-xml-dom:${treebolicVersion}"
    implementation "io.github.treebolic:provider-xml-sax:${treebolicVersion}"
    implementation "io.github.treebolic:provider-text-indent-tree:${treebolicVersion}"
    implementation "io.github.treebolic:provider-text-indent:${treebolicVersion}"
    implementation "io.github.treebolic:provider-text-pair:${treebolicVersion}"
    implementation "io.github.treebolic:provider-graphviz:${treebolicVersion}"

    implementation project(':treebolicGlue')
    implementation project(':treebolicParcel')
    implementation project(':treebolicIface')
    implementation project(':treebolicClientsIface')
    implementation project(':treebolicClientsLib')
    implementation project(':treebolicServicesIface')

    implementation project(':commonLib')
    implementation project(':storageLib')
    implementation project(':searchLib')
    implementation project(':fileChooserLib')
    implementation project(':downloadLib')
    implementation project(':preferenceLib')
    implementation project(':guideLib')
    implementation project(':rateLib')
    implementation project(':othersLib')
    implementation project(':donateLib')

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'

    implementation "androidx.multidex:multidex:${multiDexVersion}"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${desugaringVersion}"

    testImplementation 'androidx.test.ext:junit:1.2.1'
}